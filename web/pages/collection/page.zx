const std = @import("std");

const zx = @import("zx");
const js = zx.Client.js;

const app = @import("app");
const database = @import("database");

const wasm = @import("../../wasm.zig");

// TODO: move to database.Tracked
fn isTracked(species: database.Species, all_tracked: []const database.Tracked) bool {
    for (all_tracked) |tracked| {
        if (tracked.species_dex == species.pokedex) {
            return tracked.tracked;
        }
    }

    return false;
}

fn onCompletion(_: js.Object) !void {
    const window: js.Object = try js.global.get(js.Object, "window");
    const location: js.Object = try window.get(js.Object, "location");
    try location.call(void, "reload", .{});
}

fn onClickInner(ctx: zx.EventContext) !void {
    const target = ctx.getEvent().getTarget() orelse return;

    const url = try target.callAlloc(
        js.String,
        wasm.allocator,
        "getAttribute",
        .{js.string("url")},
    );

    const pokedex = try target.callAlloc(
        js.String,
        wasm.allocator,
        "getAttribute",
        .{js.string("pokedex")},
    );

    const api = try std.fmt.allocPrint(
        wasm.allocator,
        "{s}/{s}",
        .{ url, pokedex },
    );
    defer wasm.allocator.free(api);

    try wasm.api.execute(api, onCompletion);
}

fn onClick(ctx: zx.EventContext) void {
    if (zx.platform != .browser) return;
    onClickInner(ctx) catch {};
}

const ButtonProps = struct {
    is_tracked: bool,
    pokedex: i64, // should be database.Int but WASM can't "link" `database` module
    text: []const u8,
};

pub fn Button(allocator: zx.Allocator, props: ButtonProps) !zx.Component {
    // NOTE: `/` in URL will be added in onClick's handler
    const class, const url = if (props.is_tracked)
        .{ "text-green-600", "/api/untrack" }
    else
        .{ "", "/api/track" };

    return (
        <button @{allocator} onclick={onClick} {class} {url} pokedex={props.pokedex}>
            {props.text}
        </button>
    );
}

pub fn Buttons(
    allocator: zx.Allocator,
    props: struct { buttons: []const ButtonProps },
) !zx.Component {
    return (
        <div @{allocator} class="flex flex-col">
            {for (props.buttons) |button| (
                <Button {..button} />
            )}
        </div>
    );
}

pub fn Page(ctx: app.PageCtx) !zx.Component {
    var session = try ctx.app.pool.getSession(ctx.arena);
    defer session.deinit();

    const user = try ctx.state.getUser(&session) orelse {
        return (
            <>
                Must be logged in to access this page
            </>
        );
    };

    const all_tracked = try session
        .query(database.Tracked)
        .where("user_id", user.id)
        .orderBy(.species_dex, .asc)
        .findAll();

    const all_species = try session
        .query(database.Species)
        .orderBy(.pokedex, .asc)
        .findAll();

    const buttons = try ctx.arena.alloc(ButtonProps, all_species.len);
    for (all_species, 0..) |species, i| {
        buttons[i] = .{
            .is_tracked = isTracked(species, all_tracked),
            .pokedex = species.pokedex,
            .text = species.name,
        };
    }

    return (
        <div @allocator={ctx.allocator} class="flex flex-col">
            <a href="/progress">
                See your progress
            </a>

            <Buttons @rendering={.client} {buttons} />
        </div>
    );
}
