const std = @import("std");

const zx = @import("zx");
const js = zx.Client.js;

const api = @import("api");

const wasm = @import("../wasm.zig");

const dialog_id = "auth-dialog";
const submit_id = "submit-button";

const Action = enum {
    signin,
    login,
};

const UserData = struct {
    username: []const u8,
    token: []const u8,
};

var action: ?Action = null;
var user_data: ?UserData = null;

fn showForm() void {
    const a = action orelse {
        wasm.js.log(.{ js.string("action is null") }) catch {};
        return;
    };

    if (wasm.html.getElementById(dialog_id)) |dialog| {
        defer dialog.deinit();
        dialog.call(void, "showModal", .{}) catch return;
    }

    if (wasm.html.getElementById(submit_id)) |submit| {
        defer submit.deinit();
        const text = switch (a) {
            .signin => "Sign in",
            .login => "Log in",
        };
        submit.set("innerText", js.string(text)) catch return;
    }
}

fn openSignin(_: zx.EventContext) void {
    action = .signin;
    showForm();
}

fn openLogin(_: zx.EventContext) void {
    action = .login;
    showForm();
}

fn inCallback(response: api.signin.Output) !void {
    const username = try wasm.allocator.dupe(u8, response.username);
    errdefer wasm.allocator.free(username);

    const token = try wasm.allocator.dupe(u8, response.token);
    errdefer wasm.allocator.free(token);

    user_data = .{
        .username = username,
        .token = token,
    };
}

fn authenticate(_: zx.EventContext) void {
    const a = action orelse {
        wasm.js.log(.{ js.string("action is null") }) catch {};
        return;
    };

    const location: js.Object = js.global.get(js.Object, "location") catch return;
    defer location.deinit();

    const referrer: []const u8 = location.getAlloc(js.String, wasm.allocator, "pathname") catch return;
    defer wasm.allocator.free(referrer);

    const username_input: js.Object = wasm.html.getElementById("username") orelse return;
    defer username_input.deinit();
    const username: []const u8 = username_input.getAlloc(js.String, wasm.allocator, "value") catch return;
    defer wasm.allocator.free(username);

    const password_input: js.Object = wasm.html.getElementById("password") orelse return;
    defer password_input.deinit();
    const password: []const u8 = password_input.getAlloc(js.String, wasm.allocator, "value") catch return;
    defer wasm.allocator.free(password);

    switch (a) {
        .signin => wasm.api.execute(
            api.signin,
            wasm.allocator,
            "/api/signin",
            inCallback,
            .{
                .username = username,
                .password = password,
                .referrer = referrer,
            },
        ) catch return,
        .login => wasm.api.execute(
            api.login,
            wasm.allocator,
            "/api/login",
            inCallback,
            .{
                .username = username,
                .password = password,
                .referrer = referrer,
            },
        ) catch return,
    }
}

fn outCallback(response: api.logout.Output) !void {
    if (response.ok != 1) {
        wasm.js.log(.{ js.string("API's ok field should always be 1") }) catch {};
        return;
    }

    if (user_data) |data| {
        wasm.allocator.free(data.username);
        wasm.allocator.free(data.token);
    }

    user_data = null;
}

fn logout(_: zx.EventContext) void {
    const token = if (user_data) |data| data.token else {
        wasm.js.log(.{ js.string("user_data is null") }) catch {};
        return;
    };

    wasm.api.execute(
        api.logout,
        wasm.allocator,
        "/api/logout",
        outCallback,
        .{
            .token = token,
        },
    ) catch {};
}

fn closeForm(_: zx.EventContext) void {
    action = null;

    if (wasm.html.getElementById(dialog_id)) |dialog| {
        defer dialog.deinit();
        dialog.call(void, "close", .{}) catch {};
    }

    if (wasm.html.getElementById("username")) |username_input| {
        defer username_input.deinit();
        username_input.set("value", js.string("")) catch {};
    }

    if (wasm.html.getElementById("password")) |password_input| {
        defer password_input.deinit();
        password_input.set("value", js.string("")) catch {};
    }
}

pub fn Buttons(allocator: zx.Allocator) zx.Component {
    if (!wasm.inClient()) {
        return (<></>);
    }

    const class = "text-primary bg-secondary rounded-md";

    return (
        <div @{allocator}>
            {if (user_data) |data| (
                <>
                    Welcome back, {data.username}
                    <button {class} onclick={logout}>Log out</button>
                </>
            ) else (
                <>
                    <button class=`{class} mr-2` onclick={openLogin}>Log in</button>
                    <button {class} onclick={openSignin}>Sign in</button>
                </>
            )}
        </div>
    );
}

pub fn Form(allocator: zx.Allocator) zx.Component {
    if (!wasm.inClient()) {
        return (<></>);
    }

    return (
        <dialog @{allocator} id={dialog_id} class="backdrop:bg-black/50 rounded-lg ">
            <div class="bg-secondary text-primary rounded-lg shadow-xl p-6 min-w-75 max-w-[95vw]">
                <fieldset>
                    <legend>Username</legend>
                    <input id="username" name="username" type="text" required />
                </fieldset>

                <fieldset>
                    <legend>Password</legend>
                    <input id="password" name="password" type="password" required />
                </fieldset>

                <div class="flex gap-2 mt-4">
                    <button id={submit_id} type="button" onclick={authenticate} class="bg-accent3 rounded-3xl px-4 py-2" />

                    <button type="button" class="bg-gray-500 rounded-3xl px-4 py-2" onclick={closeForm}>
                        Cancel
                    </button>
                </div>
            </div>
        </dialog>
    );
}
